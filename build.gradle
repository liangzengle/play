import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url 'https://maven.aliyun.com/repository/public/'
        }
        maven {
            url 'https://maven.aliyun.com/repository/gradle-plugin'
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath libs.kotlin.gradle
        classpath libs.kotlin.allopen
        classpath libs.ktlint.gradle
    }
}

configure(subprojects) {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url 'https://maven.aliyun.com/repository/public/'
        }
    }
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'org.jetbrains.kotlin.kapt'
    apply plugin: "org.jlleitschuh.gradle.ktlint-idea"
    apply plugin: "kotlin-spring"


    ktlint {
//        debug = true
        verbose = true
    }

    group 'play'
    version '1.0-SNAPSHOT'

    dependencies {
        api libs.bundles.kotlin
//        testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"

        api libs.auto.service
        kapt libs.auto.service

        testImplementation libs.bundles.junit
    }

    test {
        useJUnitPlatform()
        testLogging {
            exceptionFormat = TestExceptionFormat.FULL
            events(TestLogEvent.SKIPPED, TestLogEvent.FAILED)
            showExceptions = true
            showCauses = true
            showStackTraces = true
        }
    }

    def javaVersion = JavaVersion.VERSION_17

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    def kotlinCompilerArgs = [
            "-Xallow-result-return-type",
            "-XXLanguage:+InlineClasses",
            "-Xopt-in=kotlin.RequiresOptIn",
            "-Xopt-in=kotlin.ExperimentalUnsignedTypes",
            "-Xopt-in=kotlin.time.ExperimentalTime",
            "-Xopt-in=kotlin.contracts.ExperimentalContracts",
            "-Xopt-in=kotlin.experimental.ExperimentalTypeInference",
            "-Xopt-in=kotlin.io.path.ExperimentalPathApi",
            "-Xopt-in=kotlin.ExperimentalStdlibApi",
            "-Xopt-in=kotlinx.serialization.ExperimentalSerializationApi",
            "-Xopt-in=kotlinx.serialization.InternalSerializationApi",
            "-Xjvm-default=all",
            "-Xstring-concat=indy-with-constants"
    ]

    compileKotlin {
        kotlinOptions {
            jvmTarget = javaVersion
            javaParameters = true
            freeCompilerArgs = kotlinCompilerArgs
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = javaVersion
            javaParameters = true
            freeCompilerArgs = kotlinCompilerArgs
        }
    }

    compileJava {
        options.encoding = "UTF-8"
        options.compilerArgs << '-parameters'
    }

    compileTestJava {
        options.encoding = "UTF-8"
        options.compilerArgs << '-parameters'
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }
}