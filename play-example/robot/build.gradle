apply plugin: 'idea'

sourceSets {
    codegen {
        kotlin.srcDir 'src/codegen/kotlin'
    }
    main.kotlin.srcDirs += "build/generated-sources"
}

idea.module {
    scopes.PROVIDED.plus += [configurations.codegenCompileClasspath]
}

dependencies {
    api project(':play-example-game')

    codegenImplementation project(':play-example-game')
    codegenImplementation Deps.KotlinPoetDeprecated
}

configurations.all {
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-webflux'
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-actuator'
    exclude group: 'com.alibaba.rsocket', module: '*'
}

task generateCaches(type: JavaExec) {
    main = 'play.example.robot.codegen.Generator'
    classpath = sourceSets.codegen.runtimeClasspath
    args "${buildDir}/generated-sources/"

    outputs.upToDateWhen { !tasks.compileCodegenJava.didWork }
    outputs.dir "${buildDir}/generated-sources/"
    outputs.cacheIf { true }
}
compileJava.dependsOn(generateCaches)
compileKotlin.dependsOn(generateCaches)